// SPDX-License-Identifier: MIT
pragma solidity =0.8.6;

interface IERC20 {
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount) external returns (bool);
    function allowance(address owner, address spender) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);
    event Transfer(address indexed from, address indexed to, uint256 value);
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        assembly {
            size := extcodesize(account)
        }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(isContract(target), "Address: delegate call to non-contract");

        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}

/**
 * @dev Implementation of the {IERC20} interface.
 *
 * This implementation is agnostic to the way tokens are created. This means
 * that a supply mechanism has to be added in a derived contract using {_mint}.
 * For a generic mechanism see {ERC20PresetMinterPauser}.
 *
 * TIP: For a detailed writeup see our guide
 * https://forum.zeppelin.solutions/t/how-to-implement-erc20-supply-mechanisms/226[How
 * to implement supply mechanisms].
 *
 * We have followed general OpenZeppelin Contracts guidelines: functions revert
 * instead returning `false` on failure. This behavior is nonetheless
 * conventional and does not conflict with the expectations of ERC20
 * applications.
 *
 * Additionally, an {Approval} event is emitted on calls to {transferFrom}.
 * This allows applications to reconstruct the allowance for all accounts just
 * by listening to said events. Other implementations of the EIP may not emit
 * these events, as it isn't required by the specification.
 *
 * Finally, the non-standard {decreaseAllowance} and {increaseAllowance}
 * functions have been added to mitigate the well-known issues around setting
 * allowances. See {IERC20-approve}.
 */
contract ERC20 is Context, IERC20, IERC20Metadata {
    mapping(address => uint256) private _balances;

    mapping(address => mapping(address => uint256)) private _allowances;

    uint256 private _totalSupply;

    string private _name;
    string private _symbol;

    /**
     * @dev Sets the values for {name} and {symbol}.
     *
     * The default value of {decimals} is 18. To select a different value for
     * {decimals} you should overload it.
     *
     * All two of these values are immutable: they can only be set once during
     * construction.
     */
    constructor(string memory name_, string memory symbol_) {
        _name = name_;
        _symbol = symbol_;
    }

    /**
     * @dev Returns the name of the token.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev Returns the symbol of the token, usually a shorter version of the
     * name.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev Returns the number of decimals used to get its user representation.
     * For example, if `decimals` equals `2`, a balance of `505` tokens should
     * be displayed to a user as `5.05` (`505 / 10 ** 2`).
     *
     * Tokens usually opt for a value of 18, imitating the relationship between
     * Ether and Wei. This is the value {ERC20} uses, unless this function is
     * overridden;
     *
     * NOTE: This information is only used for _display_ purposes: it in
     * no way affects any of the arithmetic of the contract, including
     * {IERC20-balanceOf} and {IERC20-transfer}.
     */
    function decimals() public view virtual override returns (uint8) {
        return 18;
    }

    /**
     * @dev See {IERC20-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        return _totalSupply;
    }

    /**
     * @dev See {IERC20-balanceOf}.
     */
    function balanceOf(address account) public view virtual override returns (uint256) {
        return _balances[account];
    }

    /**
     * @dev See {IERC20-transfer}.
     *
     * Requirements:
     *
     * - `recipient` cannot be the zero address.
     * - the caller must have a balance of at least `amount`.
     */
    function transfer(address recipient, uint256 amount) public virtual override returns (bool) {
        _transfer(_msgSender(), recipient, amount);
        return true;
    }

    /**
     * @dev See {IERC20-allowance}.
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        return _allowances[owner][spender];
    }

    /**
     * @dev See {IERC20-approve}.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function approve(address spender, uint256 amount) public virtual override returns (bool) {
        _approve(_msgSender(), spender, amount);
        return true;
    }

    /**
     * @dev See {IERC20-transferFrom}.
     *
     * Emits an {Approval} event indicating the updated allowance. This is not
     * required by the EIP. See the note at the beginning of {ERC20}.
     *
     * Requirements:
     *
     * - `sender` and `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     * - the caller must have allowance for ``sender``'s tokens of at least
     * `amount`.
     */
    function transferFrom(
        address sender,
        address recipient,
        uint256 amount
    ) public virtual override returns (bool) {
        _transfer(sender, recipient, amount);

        uint256 currentAllowance = _allowances[sender][_msgSender()];
        require(currentAllowance >= amount, "ERC20: transfer amount exceeds allowance");
        unchecked {
            _approve(sender, _msgSender(), currentAllowance - amount);
        }

        return true;
    }

    /**
     * @dev Atomically increases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     */
    function increaseAllowance(address spender, uint256 addedValue) public virtual returns (bool) {
        _approve(_msgSender(), spender, _allowances[_msgSender()][spender] + addedValue);
        return true;
    }

    /**
     * @dev Atomically decreases the allowance granted to `spender` by the caller.
     *
     * This is an alternative to {approve} that can be used as a mitigation for
     * problems described in {IERC20-approve}.
     *
     * Emits an {Approval} event indicating the updated allowance.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `spender` must have allowance for the caller of at least
     * `subtractedValue`.
     */
    function decreaseAllowance(address spender, uint256 subtractedValue) public virtual returns (bool) {
        uint256 currentAllowance = _allowances[_msgSender()][spender];
        require(currentAllowance >= subtractedValue, "ERC20: decreased allowance below zero");
        unchecked {
            _approve(_msgSender(), spender, currentAllowance - subtractedValue);
        }

        return true;
    }

    /**
     * @dev Moves `amount` of tokens from `sender` to `recipient`.
     *
     * This internal function is equivalent to {transfer}, and can be used to
     * e.g. implement automatic token fees, slashing mechanisms, etc.
     *
     * Emits a {Transfer} event.
     *
     * Requirements:
     *
     * - `sender` cannot be the zero address.
     * - `recipient` cannot be the zero address.
     * - `sender` must have a balance of at least `amount`.
     */
    function _transfer(
        address sender,
        address recipient,
        uint256 amount
    ) internal virtual {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(sender, recipient, amount);

        uint256 senderBalance = _balances[sender];
        require(senderBalance >= amount, "ERC20: transfer amount exceeds balance");
        unchecked {
            _balances[sender] = senderBalance - amount;
        }
        _balances[recipient] += amount;

        emit Transfer(sender, recipient, amount);

        _afterTokenTransfer(sender, recipient, amount);
    }

    /** @dev Creates `amount` tokens and assigns them to `account`, increasing
     * the total supply.
     *
     * Emits a {Transfer} event with `from` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     */
    function _mint(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: mint to the zero address");

        _beforeTokenTransfer(address(0), account, amount);

        _totalSupply += amount;
        _balances[account] += amount;
        emit Transfer(address(0), account, amount);

        _afterTokenTransfer(address(0), account, amount);
    }

    /**
     * @dev Destroys `amount` tokens from `account`, reducing the
     * total supply.
     *
     * Emits a {Transfer} event with `to` set to the zero address.
     *
     * Requirements:
     *
     * - `account` cannot be the zero address.
     * - `account` must have at least `amount` tokens.
     */
    function _burn(address account, uint256 amount) internal virtual {
        require(account != address(0), "ERC20: burn from the zero address");

        _beforeTokenTransfer(account, address(0), amount);

        uint256 accountBalance = _balances[account];
        require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
        unchecked {
            _balances[account] = accountBalance - amount;
        }
        _totalSupply -= amount;

        emit Transfer(account, address(0), amount);

        _afterTokenTransfer(account, address(0), amount);
    }

    /**
     * @dev Sets `amount` as the allowance of `spender` over the `owner` s tokens.
     *
     * This internal function is equivalent to `approve`, and can be used to
     * e.g. set automatic allowances for certain subsystems, etc.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `owner` cannot be the zero address.
     * - `spender` cannot be the zero address.
     */
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual {
        require(owner != address(0), "ERC20: approve from the zero address");
        require(spender != address(0), "ERC20: approve to the zero address");

        _allowances[owner][spender] = amount;
        emit Approval(owner, spender, amount);
    }

    /**
     * @dev Hook that is called before any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * will be transferred to `to`.
     * - when `from` is zero, `amount` tokens will be minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}

    /**
     * @dev Hook that is called after any transfer of tokens. This includes
     * minting and burning.
     *
     * Calling conditions:
     *
     * - when `from` and `to` are both non-zero, `amount` of ``from``'s tokens
     * has been transferred to `to`.
     * - when `from` is zero, `amount` tokens have been minted for `to`.
     * - when `to` is zero, `amount` of ``from``'s tokens have been burned.
     * - `from` and `to` are never both zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual {}
}

contract Ownable {
    address private _owner;
    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);
    constructor () {
        _owner = msg.sender;
        emit OwnershipTransferred(address(0), msg.sender);
    }
    function owner() public view virtual returns (address) {
        return _owner;
    }
    modifier onlyOwner() {
        require(owner() == msg.sender, "Ownable: caller is not the owner");
        _;
    }
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}

contract ERC20WithVoting is ERC20 {

    constructor(string memory name_, string memory symbol_) 
        ERC20(name_, symbol_)
    {
    }

    /// @notice A record of each accounts delegate
    mapping (address => address) internal _delegates;

    /// @notice A checkpoint for marking number of votes from a given block
    struct Checkpoint {
        uint32 fromBlock;
        uint256 votes;
    }

    /// @notice A record of votes checkpoints for each account, by index
    mapping (address => mapping (uint32 => Checkpoint)) public checkpoints;

    /// @notice The number of checkpoints for each account
    mapping (address => uint32) public numCheckpoints;

    /// @notice The EIP-712 typehash for the contract's domain
    bytes32 public constant DOMAIN_TYPEHASH = keccak256("EIP712Domain(string name,uint256 chainId,address verifyingContract)");

    /// @notice The EIP-712 typehash for the delegation struct used by the contract
    bytes32 public constant DELEGATION_TYPEHASH = keccak256("Delegation(address delegatee,uint256 nonce,uint256 expiry)");

    /// @notice A record of states for signing / validating signatures
    mapping (address => uint) public nonces;

      /// @notice An event thats emitted when an account changes its delegate
    event DelegateChanged(address indexed delegator, address indexed fromDelegate, address indexed toDelegate);

    /// @notice An event thats emitted when a delegate account's vote balance changes
    event DelegateVotesChanged(address indexed delegate, uint previousBalance, uint newBalance);

    /**
     * @notice Delegate votes from `msg.sender` to `delegatee`
     * @param delegator The address to get delegatee for
     */
    function delegates(address delegator)
        external
        view
        returns (address)
    {
        return _delegates[delegator];
    }

  /**
    * @notice Delegate votes from `msg.sender` to `delegatee`
    * @param delegatee The address to delegate votes to
    */
    function delegate(address delegatee) external {
        return _delegate(msg.sender, delegatee);
    }

    /**
     * @notice Delegates votes from signatory to `delegatee`
     * @param delegatee The address to delegate votes to
     * @param nonce The contract state required to match the signature
     * @param expiry The time at which to expire the signature
     * @param v The recovery byte of the signature
     * @param r Half of the ECDSA signature pair
     * @param s Half of the ECDSA signature pair
     */
    function delegateBySig(
        address delegatee,
        uint nonce,
        uint expiry,
        uint8 v,
        bytes32 r,
        bytes32 s
    )
        external
    {
        bytes32 domainSeparator = keccak256(
            abi.encode(
                DOMAIN_TYPEHASH,
                keccak256(bytes(name())),
                getChainId(),
                address(this)
            )
        );

        bytes32 structHash = keccak256(
            abi.encode(
                DELEGATION_TYPEHASH,
                delegatee,
                nonce,
                expiry
            )
        );

        bytes32 digest = keccak256(
            abi.encodePacked(
                "\x19\x01",
                domainSeparator,
                structHash
            )
        );

        address signatory = ecrecover(digest, v, r, s);
        require(signatory != address(0), "XPEFI::delegateBySig: invalid signature");
        require(nonce == nonces[signatory]++, "XPEFI::delegateBySig: invalid nonce");
        require(block.timestamp <= expiry, "XPEFI::delegateBySig: signature expired");
        return _delegate(signatory, delegatee);
    }

    /**
     * @notice Gets the current votes balance for `account`
     * @param account The address to get votes balance
     * @return The number of current votes for `account`
     */
    function getCurrentVotes(address account)
        external
        view
        returns (uint256)
    {
        uint32 nCheckpoints = numCheckpoints[account];
        return nCheckpoints > 0 ? checkpoints[account][nCheckpoints - 1].votes : 0;
    }

    /**
     * @notice Determine the prior number of votes for an account as of a block number
     * @dev Block number must be a finalized block or else this function will revert to prevent misinformation.
     * @param account The address of the account to check
     * @param blockNumber The block number to get the vote balance at
     * @return The number of votes the account had as of the given block
     */
    function getPriorVotes(address account, uint blockNumber)
        external
        view
        returns (uint256)
    {
        require(blockNumber < block.number, "XPEFI::getPriorVotes: not yet determined");

        uint32 nCheckpoints = numCheckpoints[account];
        if (nCheckpoints == 0) {
            return 0;
        }

        // First check most recent balance
        if (checkpoints[account][nCheckpoints - 1].fromBlock <= blockNumber) {
            return checkpoints[account][nCheckpoints - 1].votes;
        }

        // Next check implicit zero balance
        if (checkpoints[account][0].fromBlock > blockNumber) {
            return 0;
        }

        uint32 lower = 0;
        uint32 upper = nCheckpoints - 1;
        while (upper > lower) {
            uint32 center = upper - (upper - lower) / 2; // ceil, avoiding overflow
            Checkpoint memory cp = checkpoints[account][center];
            if (cp.fromBlock == blockNumber) {
                return cp.votes;
            } else if (cp.fromBlock < blockNumber) {
                lower = center;
            } else {
                upper = center - 1;
            }
        }
        return checkpoints[account][lower].votes;
    }

    function _delegate(address delegator, address delegatee)
        internal
    {
        address currentDelegate = _delegates[delegator];
        uint256 delegatorBalance = balanceOf(delegator); // balance of underlying XPEFI (not scaled);
        _delegates[delegator] = delegatee;

        emit DelegateChanged(delegator, currentDelegate, delegatee);

        _moveDelegates(currentDelegate, delegatee, delegatorBalance);
    }

    function _moveDelegates(address srcRep, address dstRep, uint256 amount) internal {
        if (srcRep != dstRep && amount > 0) {
            if (srcRep != address(0)) {
                // decrease old representative
                uint32 srcRepNum = numCheckpoints[srcRep];
                uint256 srcRepOld = srcRepNum > 0 ? checkpoints[srcRep][srcRepNum - 1].votes : 0;
                uint256 srcRepNew = (srcRepOld - amount);
                _writeCheckpoint(srcRep, srcRepNum, srcRepOld, srcRepNew);
            }

            if (dstRep != address(0)) {
                // increase new representative
                uint32 dstRepNum = numCheckpoints[dstRep];
                uint256 dstRepOld = dstRepNum > 0 ? checkpoints[dstRep][dstRepNum - 1].votes : 0;
                uint256 dstRepNew = (dstRepOld + amount);
                _writeCheckpoint(dstRep, dstRepNum, dstRepOld, dstRepNew);
            }
        }
    }

    function _writeCheckpoint(
        address delegatee,
        uint32 nCheckpoints,
        uint256 oldVotes,
        uint256 newVotes
    )
        internal
    {
        uint32 blockNumber = safe32(block.number, "XPEFI::_writeCheckpoint: block number exceeds 32 bits");

        if (nCheckpoints > 0 && checkpoints[delegatee][nCheckpoints - 1].fromBlock == blockNumber) {
            checkpoints[delegatee][nCheckpoints - 1].votes = newVotes;
        } else {
            checkpoints[delegatee][nCheckpoints] = Checkpoint(blockNumber, newVotes);
            numCheckpoints[delegatee] = nCheckpoints + 1;
        }

        emit DelegateVotesChanged(delegatee, oldVotes, newVotes);
    }

    function safe32(uint n, string memory errorMessage) internal pure returns (uint32) {
        require(n < 2**32, errorMessage);
        return uint32(n);
    }

    function getChainId() internal view returns (uint256) {
        return block.chainid;
    }

    function _mint(address account, uint256 amount) internal override {
        super._mint(account, amount);
        _moveDelegates(address(0), _delegates[account], amount);
    }

    function _burn(address account, uint256 amount) internal override {
        super._burn(account, amount);
        _moveDelegates(address(0), _delegates[account], amount);
    }

    function _transfer(address sender, address recipient, uint256 amount) internal virtual override {
        super._transfer(sender, recipient, amount);
        _moveDelegates(_delegates[sender], _delegates[recipient], amount);
    }
}

// This contract handles swapping to and from iPEFI, PenguinFinances's staking token.
contract PenguinNestsV2 is ERC20WithVoting("PenguinNestsV2", "iPEFI"), Ownable {
    using SafeERC20 for IERC20;

    IERC20 public immutable pefi;

    uint256 public constant MAX_PAPER_HANDS_PENALTY = 2500;
    uint256 public constant MAX_BIPS = 10000;
    uint256 public constant SECONDS_PER_DAY = 86400;

    // LUCKY PENGUIN
    // It's a small minigame to incentivize deposits by choosing a luckyPenguin every certain (luckyPenguinInterval) amount of
    // deposits. The luckyPenguin is entitled to a percentage of the PPH. This mechanic is meant for seasonal events and others
    // community events that penguins will come up with. It can be disabled at any moment by setting isLuckyPenguinEnabled to false.

    bool public isLuckyPenguinEnabled; //Tracks wether the system is enabled.
    address public luckyPenguin; // The address that holds the luckyPenguin title.
    uint256 public luckyPenguinCount; // Tracks how many deposits have happened that are >= luckyPenguinMinimumDeposit.
    uint256 public luckyPenguinInterval; //Sets the amount of deposits before a new luckyPenguin is chosen.
    uint256 public luckyPenguinMinimumDeposit; // The threshold to be taken into consideration for the luckyPenguin title. **SET IN WEI**
    uint256 public luckyPenguinRewardBP; // split of PHP that goes to luckyPenguin

    // This is a variable that can be changed by the admin
    uint256 public paperHandsPenalty;
    // Tracks total PEFI that has been collected via the PHP.
    uint256 public fundsCollectedByPHP;
    // tracks total deposits over all time
    uint256 public totalDeposits;
    // tracks total withdrawals over all time
    uint256 public totalWithdrawals;

    //stored historic exchange rates and their timestamps, separated by ~24 hour intervals
    uint256 public numStoredExchangeRates;
    uint256[] public historicExchangeRates;
    uint256[] public historicTimestamps;

    //for tracking rewards sent to nest in trailing 24 hour period
    uint256 public rollingStartTimestamp;
    uint256 public rollingStartPefiBalance;
    uint256 public rollingStartTotalDeposits;
    uint256 public rollingStartTotalWithdrawals;

    //stores deposits to help tracking of profits
    mapping(address => uint256) public deposits;
    //stores withdrawals to help tracking of profits
    mapping(address => uint256) public withdrawals;
    //tracks earnings for lucky penguins
    mapping(address => uint256) public luckyPenguinEarnings;

    event Enter(address indexed penguin, uint256 amount);
    event Leave(address indexed penguin, uint256 amount, uint256 shares);
    event PaperHandsPenaltySet(uint256 newPaperHandsPenalty);
    event NewLuckyPenguin(address indexed previousLuckyPenguin, address indexed newLuckyPenguin);
    event LuckyPenguinDistribution(address indexed luckyPenguin, uint256 amountPefi);
    event DailyRewardsReceived(uint256 indexed timestamp, uint256 amountPefi);

    // Make sure that the PenguinToken contract we are passing in here
    // has the function `burnOwnTokens` which will burn tokens of your own
    // address.
    constructor(IERC20 _pefi, uint256 _paperHandsPenalty) {
        pefi = _pefi;
        setPaperHandsPenalty(_paperHandsPenalty);
        //must be set on deployment, in order to avoid errors for send to zero address in future
        luckyPenguin = msg.sender;
        luckyPenguinInterval = 7;
        luckyPenguinMinimumDeposit = 100e18;
        numStoredExchangeRates = 1;
        historicExchangeRates.push(1e18);
        historicTimestamps.push(block.timestamp);
        rollingStartTimestamp = block.timestamp;
    }

    //returns current exchange rate of iPEFI to PEFI, scaled up by 1e18
    function currentExchangeRate() public view returns(uint256) {
        uint256 totalShares = totalSupply();
        if(totalShares == 0) {
            return 1e18;
        }
        return (pefiBalance() * 1e18) / totalShares;
    }

    //returns user profits in PEFI (returns zero in the case that user has losses due to previous PHP)
    function userProfits(address penguinAddress) public view returns(uint256) {
        uint256 userDeposits = deposits[penguinAddress];
        uint256 userWithdrawals = withdrawals[penguinAddress];
        uint256 totalShares = totalSupply();
        uint256 shareValue = (balanceOf(penguinAddress) * pefiBalance()) / totalShares;
        uint256 totalAssets = userWithdrawals + shareValue;
        if(totalAssets >= userDeposits) {
            return (totalAssets - userDeposits);
        } else {
            return 0;
        }
    }

    //returns most recent stored exchange rate and the time at which it was stored
    function getLatestStoredExchangeRate() public view returns(uint256, uint256) {
        return (historicExchangeRates[numStoredExchangeRates - 1], historicTimestamps[numStoredExchangeRates - 1]);
    }

    //returns last amount of stored exchange rate datas
    function getExchangeRateHistory(uint256 amount) public view returns(uint256[] memory, uint256[] memory) {
        uint256 endIndex = numStoredExchangeRates - 1;
        uint256 startIndex = (amount > endIndex) ? 0 : (endIndex - amount + 1);
        uint256 length = endIndex - startIndex + 1;
        uint256[] memory exchangeRates = new uint256[](length);
        uint256[] memory timestamps = new uint256[](length);
        for(uint256 i = startIndex; i <= endIndex; i++) {
            exchangeRates[i - startIndex] = historicExchangeRates[i];
            timestamps[i - startIndex] = historicTimestamps[i];            
        }
        return (exchangeRates, timestamps);
    }

    function pefiBalance() public view returns(uint256) {
        return pefi.balanceOf(address(this));
    }

    //tracks the amount of PEFI the nest has received over the last 24 hours
    function rewardsReceived() public view returns(uint256) {
        // Gets the current PEFI balance of the nest
        uint256 totalPefi = pefiBalance();
        // gets deposits during the period
        uint256 depositsDuringPeriod = totalDeposits - rollingStartTotalDeposits;
        // gets withdrawals during the period
        uint256 withdrawalsDuringPeriod = totalWithdrawals - rollingStartTotalWithdrawals;
        // net rewards received is (new pefi balance - old pefi balance) + (withdrawals - deposits)
        return ((totalPefi + withdrawalsDuringPeriod) - (depositsDuringPeriod + rollingStartPefiBalance));
    }

    function timeSinceLastDailyUpdate() public view returns(uint256) {
        return (block.timestamp - rollingStartTimestamp);
    }

    // Enter the nest. Pay some PEFI. Earn some shares.
    // Locks PEFI and mints iPEFI
    function enter(uint256 _amount) external {
        // Gets the amount of PEFI locked in the contract
        uint256 totalPefi = pefiBalance();
        // Gets the amount of iPEFI in existence
        uint256 totalShares = totalSupply();
        // If no iPEFI exists, mint it 1:1 to the amount put in
        if (totalShares == 0 || totalPefi == 0) {
            _mint(msg.sender, _amount);
        }
        // Calculate and mint the amount of iPEFI the PEFI is worth.
        // The ratio will change overtime, as iPEFI is burned/minted and PEFI
        // deposited + gained from fees / withdrawn.
        else {
            uint256 what = (_amount * totalShares) / totalPefi;
            _mint(msg.sender, what);
        }
        //track deposited PEFI
        deposits[msg.sender] = deposits[msg.sender] + _amount;
        totalDeposits += _amount;
        // Lock the PEFI in the contract
        pefi.safeTransferFrom(msg.sender, address(this), _amount);

        // LUCKY PENGUIN
        if (isLuckyPenguinEnabled && _amount >= luckyPenguinMinimumDeposit) {
            luckyPenguinCount++;
            if (luckyPenguinCount % luckyPenguinInterval == 0){
                emit NewLuckyPenguin(luckyPenguin, msg.sender);
                luckyPenguin = msg.sender;
            }
        }

        _dailyUpdate();

        emit Enter(msg.sender, _amount);
    }

    // Leave the vault. Claim back your PEFI.
    // Unlocks the staked + gained PEFI and redistributes iPEFI.
    function leave(uint256 _share) external {
        // Gets the amount of iPEFI in existence
        uint256 totalShares = totalSupply();
        // Gets the PEFI balance of the nest
        uint256 totalPefi = pefiBalance();  
        // Calculates the amount of PEFI the iPEFI is worth      
        uint256 what = (_share * totalPefi) / totalShares;
        //burn iPEFI
        _burn(msg.sender, _share);
        //send withdrawn PEFI
        uint256 tax = (what * paperHandsPenalty) / MAX_BIPS;
        uint256 toSend = what - tax;
        // Send the person's PEFI to their address
        pefi.safeTransfer(msg.sender, toSend);
        //track withdrawn PEFI
        withdrawals[msg.sender] += toSend;
        totalWithdrawals += toSend;

        fundsCollectedByPHP += tax;

        if (isLuckyPenguinEnabled && luckyPenguinRewardBP > 0){
            uint256 luckyPenguinReward = (tax * luckyPenguinRewardBP) / MAX_BIPS;
            luckyPenguinEarnings[luckyPenguin] = luckyPenguinEarnings[luckyPenguin] + luckyPenguinReward;
            pefi.safeTransfer(luckyPenguin, luckyPenguinReward);
            //track withdrawn PEFI
            totalWithdrawals += luckyPenguinReward;
            emit LuckyPenguinDistribution(luckyPenguin, luckyPenguinReward);
        }

         _dailyUpdate();

        emit Leave(msg.sender, what, _share);
    }

    // Change the early withdrawal amount
    function setPaperHandsPenalty(uint256 _paperHandsPenalty) public onlyOwner {
        require(_paperHandsPenalty <= MAX_PAPER_HANDS_PENALTY, "Can't have paper hands penalty greater than maximum");
        paperHandsPenalty = _paperHandsPenalty;
        emit PaperHandsPenaltySet(_paperHandsPenalty);
    }

    // Change the lucky penguin amount
    function setLuckyPenguinRewardBP(uint256 _luckyPenguinRewardBP) external onlyOwner {
        require(_luckyPenguinRewardBP <= MAX_BIPS, "Can't have luckyPenguinRewardBP greater than maximum");
        luckyPenguinRewardBP = _luckyPenguinRewardBP;
    }

    //turn lucky penguin minigame on/off
    function setLuckyPenguinEnabled(bool _luckyPenguinEnabled) external onlyOwner {
        isLuckyPenguinEnabled = _luckyPenguinEnabled;
    }

    function setLuckyPenguinInterval(uint256 _luckyPenguinInterval) external onlyOwner {
        require(_luckyPenguinInterval != 0);
        luckyPenguinInterval = _luckyPenguinInterval;
    }

    function setLuckyPenguinMinimumDeposit(uint256 _luckyPenguinMinimumDeposit) external onlyOwner {
        luckyPenguinMinimumDeposit = _luckyPenguinMinimumDeposit;
    }

    //tracking for profits on transfers
    function _transfer(
        address sender,
        address recipient,
        uint256 amount
    ) internal override {
        // Gets the amount of iPEFI in existence
        uint256 totalShares = totalSupply();
        // Gets the PEFI balance of the nest
        uint256 totalPefi = pefiBalance();
        uint256 pefiValueOfShares = (amount * totalPefi) / totalShares;
        // take part of profit tracking
        deposits[recipient] += pefiValueOfShares;
        withdrawals[sender] += pefiValueOfShares;
        //perform the internal transfer
        super._transfer(sender, recipient, amount);
    }

    function _dailyUpdate() internal {
        if (timeSinceLastDailyUpdate() >= SECONDS_PER_DAY) {
            emit DailyRewardsReceived(block.timestamp, rewardsReceived());
            //update rolling data
            rollingStartTimestamp = block.timestamp;
            rollingStartPefiBalance = pefiBalance();
            rollingStartTotalDeposits = totalDeposits;
            rollingStartTotalWithdrawals = totalWithdrawals;
            //store exchange rate and timestamp
            historicExchangeRates.push(currentExchangeRate());
            historicTimestamps.push(block.timestamp);
            numStoredExchangeRates += 1;
        }
    }
}

contract PenguinNestMigrator is Ownable {
    using SafeERC20 for IERC20;

    address public pefi;
    address public oldNest;
    address public newNest;
    uint256 internal constant MAX_UINT = 115792089237316195423570985008687907853269984665640564039457584007913129639935;

    event Migrated(address indexed penguin, uint256 amountV1XPEFI, uint256 amountPefi, uint256 amountV2XPEFI);

    constructor(address _pefi, address _oldNest, address _newNest) {
        pefi = _pefi;
        oldNest = _oldNest;
        newNest = _newNest;
        IERC20(_pefi).approve(_newNest, MAX_UINT);
    }

    function getIPEFIFromXPEFI(uint256 amountXPEFI) public view returns(uint256) {
        IERC20 previousNest = IERC20(oldNest);
        IERC20 pefiToken = IERC20(pefi);
        uint256 pefiBalanceOfOldNest = pefiToken.balanceOf(oldNest);
        uint256 totalXPEFI = previousNest.totalSupply();
        uint256 scaledExchangeRate = PenguinNestsV2(newNest).currentExchangeRate();
        uint256 pefiReceived = (amountXPEFI * pefiBalanceOfOldNest) / totalXPEFI;
        uint256 ipefiReceived = (pefiReceived * 1e18) / scaledExchangeRate;
        return ipefiReceived;
    }

    function ipefiExpectedOnMigration(address user) public view returns(uint256) {
        uint256 amountXPEFI = IERC20(oldNest).balanceOf(user);
        return getIPEFIFromXPEFI(amountXPEFI);
    }

    function migrateXPEFI() external {
        IERC20 previousNest = IERC20(oldNest);
        IERC20 updatedNest = IERC20(newNest);
        IERC20 pefiToken = IERC20(pefi);
        //get user balance of XPEFI
        uint256 amountXPEFI = previousNest.balanceOf(msg.sender);
        require(amountXPEFI > 0, "no XPEFI to migrate. Did you already complete migration?");
        //transfer XPEFI to this address
        previousNest.safeTransferFrom(msg.sender, address(this), amountXPEFI);
        //turn XPEFI into PEFI
        PenguinNestsV2(oldNest).leave(amountXPEFI);
        //get the amount of PEFI received
        uint256 amountPefi = pefiToken.balanceOf(address(this));
        //enter the new nest
        PenguinNestsV2(newNest).enter(amountPefi);
        //get the amount of IPEFI received
        uint256 ipefiReceived = updatedNest.balanceOf(address(this));
        //transfer the IPEFI to the user
        updatedNest.safeTransfer(msg.sender, ipefiReceived);
        emit Migrated(msg.sender, amountXPEFI, amountPefi, ipefiReceived);
    }

    function inCaseTokensGetStuck(IERC20 token, address to, uint256 amount) external onlyOwner {
        require(amount > 0, "cannot recover 0 tokens");
        token.safeTransfer(to, amount);
    }
}


